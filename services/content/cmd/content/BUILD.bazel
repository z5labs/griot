load("@aspect_bazel_lib//lib:expand_template.bzl", "expand_template")
load("@rules_go//go:def.bzl", "go_binary", "go_library")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_load", "oci_push")
load("@rules_pkg//pkg:tar.bzl", "pkg_tar")

go_library(
    name = "content_lib",
    srcs = ["main.go"],
    importpath = "github.com/z5labs/griot/services/content/cmd/content",
    visibility = ["//visibility:private"],
    deps = ["//services/content"],
)

go_binary(
    name = "content",
    embed = [":content_lib"],
    pure = "on",
    static = "on",
    visibility = ["//visibility:public"],
)

pkg_tar(
    name = "tar",
    srcs = [":content"],
)

expand_template(
    name = "labels",
    out = "_stamped.labels.txt",
    stamp_substitutions = {
        "0.0.0": "{{BUILD_EMBED_LABEL}}",
        "CREATED_TIMESTAMP": "{{BUILD_TIMESTAMP}}",
    },
    template = [
        "org.opencontainers.image.created=CREATED_TIMESTAMP",
        "org.opencontainers.image.source=https://github.com/z5labs/griot",
        "org.opencontainers.image.version=0.0.0",
        "org.opencontainers.image.licenses=Apache-2.0",
        "org.opencontainers.image.base.name=gcr.io/distroless/static",
        "org.opencontainers.image.base.digest=sha256:cc226ca14d17d01d4b278d9489da930a0dd11150df10ae95829d13e6d00fbdbf",
    ],
)

oci_image(
    name = "image",
    base = "@distroless_static",
    entrypoint = ["/content"],
    labels = ":labels",
    tars = [":tar"],
)

oci_load(
    name = "load",
    image = ":image",
    repo_tags = ["ghcr.io/z5labs/griot/content:latest"],
)

expand_template(
    name = "remote_tags",
    out = "_stamped.remote_tags.txt",
    stamp_substitutions = {
        "0.0.0": "{{BUILD_EMBED_LABEL}}",
    },
    template = [
        "latest",
        "0.0.0",
    ],
)

oci_push(
    name = "push",
    image = ":image",
    remote_tags = ":remote_tags",
    repository = "ghcr.io/z5labs/griot/content",
)
